plugins {
    id 'idea'
    id "io.spring.dependency-management" version "1.0.11.RELEASE"
    id 'com.github.johnrengelman.shadow' version '6.1.0' apply false
    id "me.champeau.jmh" version "0.6.6"
}

idea {
    project {
        languageLevel = 17
    }
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

allprojects {
    group "ru.otus"

    repositories {
        mavenLocal()
        mavenCentral()
    }

    apply plugin: "io.spring.dependency-management"
    dependencyManagement {
        dependencies {
            imports {
                mavenBom("org.springframework.boot:spring-boot-dependencies:2.5.2")
            }
            dependency("org.projectlombok:lombok:${Versions.lombok}")
            dependency("org.ow2.asm:asm-commons:${Versions.asm}")
            dependency("com.google.guava:guava:${Versions.guava}")
            dependency("com.ibm.icu:icu4j:${Versions.icu4j}")
            dependency("org.glassfish:javax.json:${Versions.glassfishJson}")
            dependency("com.google.protobuf:protobuf-java-util:${Versions.protobuf}")
            dependency("org.testcontainers:junit-jupiter:${Versions.testcontainers}")
            dependency("org.testcontainers:postgresql:${Versions.testcontainers}")
        }
    }

    configurations.all {
        resolutionStrategy {
            failOnVersionConflict()
        }
        resolutionStrategy {
            force("javax.servlet:servlet-api:2.4")
            force("commons-logging:commons-logging:1.1.1")
            force("commons-lang:commons-lang:2.5")
            force("org.codehaus.jackson:jackson-core-asl:1.8.8")
            force("org.codehaus.jackson:jackson-mapper-asl:1.8.3")
            force("org.codehaus.jettison:jettison:1.1")
            force("net.java.dev.jna:jna:5.8.0")
            force("com.google.errorprone:error_prone_annotations:2.11.0")
        }
    }
}

//print versions from BOM
task managedVersions {
    doLast {
        dependencyManagement.managedVersions.each {
            println it
        }
    }
}

dependencies {
    implementation 'com.github.nbbrd.java-sql-util:java-sql-jdbc:1.0.2'
    testImplementation 'org.java-sql-generator:java-sql-generator:0.2'
}
